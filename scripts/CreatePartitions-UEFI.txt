rem == CreatePartitions-UEFI.txt ==
rem == These commands are used with DiskPart to create four partitions for a UEFI/GPT-based device.
rem == Adjust the partition sizes as necessary.

rem == Clean Disk ==
rem == Clear all information on disk and convert to gpt.
select disk 0
clean
convert gpt

rem == 1. System Partition ==
rem == For Advanced Format 4Kn drives, change this value to size=260.
create partition efi size=260
format quick fs=fat32 label="System"
assign letter=S

rem == 2. Microsoft Reserved (MSR) Partition ==
create partition msr size=16

rem == 3. Windows Partition ==
rem == Create the Windows partition.
create partition primary

rem == Create space for the recovery tools
rem == Update this size to match the size of the recovery tools (winre.wim) plus some free space.
rem == Note: On Windows 11 23H2 (22621.2861), winre.wim is approximately 665 MB.
shrink minimum=1152

rem == Prepare Windows Partition
gpt attributes=0x0000000000000000
format quick fs=ntfs label="Local Disk"
assign letter=W

rem == 4. Recovery Partition ==
create partition primary size=1152
set id=DE94BBA4-06D1-4D40-A16A-BFD50179D6AC
gpt attributes=0x8000000000000001
format quick fs=ntfs label="Windows RE Tools"
assign letter=T

rem == Rescan Disks and Exit ==
rescan
exit
